# Maintainer: Spacingbat3 (https://github.com/spacingbat3)
pkgname=electron-discord-webapp-git
pkgver=v1.1.0_pre1.r67.cbfb1a7
pkgrel=1
pkgdesc="A Discord Web App based on the Electron engine."
arch=("x86_64" "i686" "aarch64" "armv7h")

_march=${CARCH}
_author="SpacingBat3"

license=('MIT')
makedepends=('npm' 'git' 'jq')
depends=('electron')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}" "discord" "discord_arch_electron")
source=("git+https://github.com/${_author}/${pkgname%-git}.git")
md5sums=('SKIP')


pkgver() {
    cd "${srcdir}/${pkgname%-git}"
    printf "v%s.r%s.%s" "$(cat ./package.json | jq -r '.version' | tr '-' '_')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgname%-git}"
    npm install --only=prod
}

build() {
    _terminal() {
        printf "#!/bin/bash\nelectron /usr/lib/${pkgname%-git}.asar\nexit \$?">"$1"
    }
    _desktop() {
        printf "[Desktop Entry]\nVersion=1.0\nTerminal=false\nType=Application\nName=Discord\nExec=/usr/bin/discord\nIcon=discord\nCategories=Network;Chat;VideoConference;WebApp;Electron\nComment=Your place to talk!\nComment[pl]=Twoje miejsce do rozmÃ³w!\nGenericName=Network Messenger\nGenericName[pl]=Kommunikator internetowy\nStartupNotify=true">"$1"
    }
    cd "${srcdir}/${pkgname%-git}"
    [[ -f docs/LICENSE.md ]] && mv docs/LICENSE.md ../ && rm -R docs
    [[ -f ../${pkgname%-git}.asar ]] && rm ../${pkgname%-git}.asar
    npx asar pack . ../${pkgname%-git}.asar
    _desktop "${srcdir}/${pkgname%-git}.desktop"
    _terminal "${srcdir}/${pkgname%-git}.sh"
}

package() {
    cd "${srcdir}"
    install -Dm755 "${srcdir}/${pkgname%-git}.asar" "${pkgdir}/usr/lib/${pkgname%-git}.asar"
    install -Dm644 "${srcdir}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname%-git}/LICENSE.md"
    install -Dm644 "${srcdir}/${pkgname%-git}/icons/app.png" "${pkgdir}/usr/share/icons/hicolor/512x512/apps/discord.png"
    install -Dm755 "${srcdir}/${pkgname%-git}.desktop" "${pkgdir}/usr/share/applications/${pkgname%-git}.desktop"   
    install -Dm755 "${srcdir}/${pkgname%-git}.sh" "${pkgdir}/usr/bin/discord"
}
